/* 
Fake Token

Strategy: 
1. on_notify function 
2. _code is not checked by either attribute or check()
*/

/***********
** Output **
************/

#if DEBUG
	.output PossibleFakeToken
	.output CheckFakeToken
#endif

.output FakeTokenViolation

/************/


/* onTransfer actions that are not checked by attribute */
.decl PossibleFakeToken(func: Function)

PossibleFakeToken(action):-
	OnTransferAction(call, action),
	!OnTransferEOSAction(call, action).

/* Mannual check _code */
.decl CheckFakeToken(insn: Insn)

// Check if _code is used for testing
// can restrict it to only "EOSIO.token"
CheckFakeToken(insn):-
	(_AssignType(insn, to, "Ne");
	_AssignType(insn, to, "Eq")),
	_AssignVar(insn, to, from),
	pointer.VarPointsTo(from, "_code").

/* safe action by either calling check directly 
or calling function with check */
.decl SafeToken(action: Function)

SafeToken(action):-
	PossibleFakeToken(action),
	_FuncInsn(action, insn),
	CheckFakeToken(insn).

SafeToken(action):-
	PossibleFakeToken(action),
	ActionFuncReachable(action, func, _),
	_FuncInsn(func, insn),
	CheckFakeToken(insn).

/* violation */
.decl FakeTokenViolation(action: Function)
FakeTokenViolation(action):-
	PossibleFakeToken(action),
	!SafeToken(action).
